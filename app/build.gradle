apply plugin: 'com.android.application'

apply from: "${rootDir}/gradle/coverage.gradle"
apply from: "${rootDir}/gradle/checkstyle.gradle"
apply from: "${rootDir}/gradle/findbugs.gradle"
apply from: "${rootDir}/gradle/jdepend.gradle"
apply from: "${rootDir}/gradle/pmd.gradle"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.fbluemle.quality"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode gitTagVersionCode
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        warningsAsErrors true
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = outputFileName.replace(".apk", "-${version}.apk")
            if (variant.buildType.isMinifyEnabled()) {
                variant.assemble.doLast {
                    variant.mappingFile.renameTo(
                            variant.mappingFile.parent + "/"
                                    + outputFileName.replace(".apk", "-")
                                    + variant.mappingFile.name)
                }
            }
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

check.dependsOn 'checkstyle', 'findbugs', 'jdependReport', 'pmd'
